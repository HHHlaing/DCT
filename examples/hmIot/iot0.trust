// Message-Based Pub/Sub 0 trust schema

// Each process has an identity associated with a particular type of
// certificate that is signed and validatable.

// trust anchor name and subdomain associated with this schema
_network:    "myNet"
_domain:     "mbps0"

// MBPS Publication definition
// target, topic, topicArgs are set using application parameters
// msgID, sCnt, mts get set in mbps.hpp (the MBPS "shim") for a particular message
// A particular application process can make its subscription more
// specific by using a subscription list with specific subCollections (designated
// by targets) and related callback functions.

#mbps0Pub: _network/_domain/target/topic/trgtLoc/topicArgs/_origin/msgID/sCnt/mts & {
    _origin:   sysId()
} <= roleCert

roleCert:    _network/_domain/_role/_roleId/_keyinfo <= netCert

netCert: _network/_domain/_keyinfo

// schema's Publication prefix and validator type
#pubPrefix:    _network/_domain
#pubValidator: "EdDSA"

// Prefix used at the syncps (NDN Interest/Data) level to sync
// this collection.
#wirePrefix: _ndnprefix/_domain & { _ndnprefix: "localnet" }
#wireValidator: "EdDSA"

// uses NDN certificate format v2 suffix for a cert name, final four
// components are KEY, keyID, issuerID, and version
_keyinfo: "KEY"/_/"dct"/_

